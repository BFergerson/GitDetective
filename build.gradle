subprojects {
    apply plugin: 'groovy'

    version = '1.3.1'
    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenCentral()
        maven { url = 'https://jitpack.io' }
        maven {
            url "https://oss.sonatype.org/content/repositories/releases"
        }
    }

    dependencies {
        compile 'org.codehaus.groovy:groovy-all:2.3.11'
        compile group: 'io.vertx', name: 'vertx-core', version: '3.5.3'
        compile 'com.github.bfergerson:vertx-kue:ff10b8d73d'
        compile group: 'ai.grakn', name: 'client-java', version: '1.3.0'
        compile 'org.mapdb:mapdb:3.0.5'
    }

    task generateSources {
        def outputDir = file("$buildDir/generated-sources")
        outputs.dir outputDir
        doFirst {
            def srcFile = new File(outputDir, "io/gitdetective/GitDetectiveVersion.groovy")
            srcFile.parentFile.mkdirs()
            srcFile.write("""package io.gitdetective
                
class GitDetectiveVersion {
    static String getVersion() {
        return "$project.version"
    }
}
                """)
        }
    }
    compileGroovy.dependsOn generateSources
    sourceSets.main.groovy.srcDirs = ['build/generated-sources', 'src/main/groovy']
}