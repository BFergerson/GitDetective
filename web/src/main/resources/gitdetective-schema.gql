define

##########---------- Entities ----------##########
user sub entity,
    key user_name,
    has create_date,
    plays has_project;
project sub entity,
    key project_name,
    has create_date,
    has reference_count,
    plays is_project,
    plays has_defines_file;
file sub entity,
    has subkind,
    has file_location,
    has qualified_name,
    has reference_count,
    plays is_defines_file,
    plays has_defines_function,
    plays has_reference_call;
function sub entity,
    key kythe_uri,
    has subkind,
    has qualified_name,
    has commit_sha1,
    has commit_date,
    has reference_count,
    plays is_defines_function,
    plays is_reference_call,
    plays has_reference_call;

##########---------- Attributes ----------##########
name sub attribute, abstract, datatype string;
create_date sub attribute, datatype date;
commit_date sub attribute, datatype date;
subkind sub attribute, datatype string;
file_location sub attribute, datatype string;
commit_sha1 sub attribute, datatype string;
user_name sub name, datatype string;
project_name sub name, datatype string;
qualified_name sub name, datatype string;
kythe_uri sub attribute, datatype string;
line_number sub attribute, datatype long;
start_offset sub attribute, datatype long;
end_offset sub attribute, datatype long;
reference_count sub attribute, datatype long;

##########---------- Relationships ----------##########
owns_project sub relation,
    relates has_project,
    relates is_project;

defines_file sub relation,
    relates has_defines_file,
    relates is_defines_file;

code_link sub relation, abstract,
    has line_number,
    has start_offset,
    has end_offset;
defines_function sub code_link,
    relates has_defines_function,
    relates is_defines_function;
reference_call sub code_link,
    relates has_reference_call,
    relates is_reference_call;
